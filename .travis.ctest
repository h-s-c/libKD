set(CTEST_SITE "Travis CI")

string(SUBSTRING "$ENV{TRAVIS_COMMIT}" 0 7 COMMIT_ID)
set(CTEST_BUILD_NAME "$ENV{TRAVIS_BUILD_NUMBER}-${COMMIT_ID}-$ENV{CC})")

set(CTEST_SOURCE_DIRECTORY "$ENV{TRAVIS_BUILD_DIR}")
set(CTEST_BINARY_DIRECTORY "${CTEST_SOURCE_DIRECTORY}/build")

configure_file(${CTEST_SOURCE_DIRECTORY}/.travis.ctest.note.in ${CTEST_BINARY_DIRECTORY}/.travis.ctest.note)
set(CTEST_NOTES_FILES ${CTEST_BINARY_DIRECTORY}/.travis.ctest.note)

set(CTEST_CMAKE_GENERATOR "Unix Makefiles")
set(CTEST_BUILD_CONFIGURATION "$ENV{CMAKE_BUILD_TYPE}")
set(CTEST_CONFIGURATION_TYPE "Debug")

find_program(CTEST_MEMORYCHECK_COMMAND "valgrind")
set(CTEST_MEMORYCHECK_COMMAND_OPTIONS "--track-origins=yes --leak-check=yes")


string(SUBSTRING "$ENV{CC}" 0 3 CC)
if(CC MATCHES "gcc")
    string(SUBSTRING "$ENV{CC}" 4 7 CC_VERSION)
    find_program(CTEST_COVERAGE_COMMAND "gcov-${CC_VERSION}")
endif()

if($ENV{CC} MATCHES "clang")
    find_program(CTEST_COVERAGE_COMMAND "llvm-cov")
endif()

set(CTEST_CUSTOM_COVERAGE_EXCLUDE ${CTEST_CUSTOM_COVERAGE_EXCLUDE} "/distribution/" "/examples/" "/thirdparty/" "/tests/" "/cov-int/" "/CMakeFiles/" "/usr/")
set(CTEST_COVERAGE_EXTRA_FLAGS "-l -p")

ctest_start(Continuous)
ctest_configure()
ctest_build()
ctest_test()
if(CTEST_MEMORYCHECK_COMMAND)
    ctest_memcheck()
endif()
if(CTEST_COVERAGE_COMMAND)
    ctest_coverage()
endif()
ctest_submit()
